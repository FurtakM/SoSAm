Export function PrepareJMMTeam; // DO POPRAWY! , poprawilem ladnie ;)
var i, j, veh, rnd, count;
begin
    uc_side = 1;
    uc_nation = 1;

    count = 0;

    JMM = PrepareUnit('10_JMM', true);
    JMMSquad = JMMSquad ^ JMM;

    Gary = PrepareUnit('10_Gary', true);
    if Gary then JMMSquad = JMMSquad ^ Gary;

    Cyrus = PrepareUnit('10_Cyrus', true);
    if Cyrus then JMMSquad = JMMSquad ^ Cyrus;

    Bobby = PrepareUnit('10_Bobby', true);
    if Bobby then JMMSquad = JMMSquad ^ Bobby;

    Lisa = PrepareUnit('10_Lisa', true);
    if Lisa then JMMSquad = JMMSquad ^ Lisa;

    Brown = PrepareUnit('10_Brown', true);
    if Brown then JMMSquad = JMMSquad ^ Brown;
    
    Donaldson = PrepareUnit('10_Donaldson', true);
    if Donaldson then JMMSquad = JMMSquad ^ Donaldson;

    Frank = PrepareUnit('10_Frank', true);
    if Frank then JMMSquad = JMMSquad ^ Frank;

    Houten = PrepareUnit('10_Houten', true);
    if Houten then JMMSquad = JMMSquad ^ Houten;

    others10 = CreateCharacterSet('OthersIn10');
    Powell = PrepareUnit('Powell', false);

    for i in JMMSquad do
    begin

        if count < 3 then
        begin
            vc_engine = 1;
            vc_control = 1;
            vc_weapon = 12;
            vc_chassis = 3;

            veh = CreateVehicle;
            SetDir(veh, 1);
            PlaceUnitArea(veh, PlayerSpawn, false);

            if count mod 2 = 0 then AddCargo(veh, 1, 100)
            else AddCargo(veh, 3, 100)
        end;

        if count >= 3 then
        begin
            vc_engine = 1;
            vc_control = 1;
            vc_weapon = [3, 4, 5][Rand(1, 3)];
            vc_chassis = 3;

            veh = CreateVehicle;
            SetDir(veh, 1);
            PlaceUnitArea(veh, PlayerSpawn, false);
        end;

        count = count + 1;
        
        PlaceHumanInUnit(i, veh);
    end;

    for i in others10 do
    begin
        vc_engine = 1;
        vc_control = 1;
        vc_weapon = [3, 4, 5][Rand(1, 3)];
        vc_chassis = 3;

        veh = CreateVehicle;
        SetDir(veh, 1);
        PlaceUnitArea(veh, PlayerSpawn, false);
        playerVehs = playerVehs ^ veh;

        PlaceHumanInUnit(i, veh);
        end;

    SetClass(FilterAllUnits([[f_side, 1], [f_type, unit_human]]), 1);
end;

Export function PrepareVehicles;
var i, veh, rnd, cargoAmmount;
begin
    uc_side = 1;
    uc_nation = 1;

    for i := 1 to 10 do
    begin
        vc_engine = 1;
        vc_control = 1;
        vc_weapon = [3, 4, 5, 12][Rand(1, 4)];
        vc_chassis = 3;

        veh = CreateVehicle;
        SetDir(veh, 1);
        PlaceUnitArea(veh, PlayerSpawn, false);
        playerVehs = playerVehs ^ veh;

        if vc_weapon = 12 then
        begin
            rnd = Rand(5, 10) * 10;
            AddCargo(veh, [1, 3][Rand(1,2)], rnd);
        end; 
            
    end;

end;

Export function PrepareCathyTeam;
var i, tmp;
begin
    uc_side = 4;
    uc_nation = 1;

    Simms = PrepareUnit('Simms', false);
    Denis = PrepareUnit('08_Denis', true);
    Joan = PrepareUnit('08_Joan', true);
    if TestCharacters('08_Khatam') then Khatam = PrepareUnit('08_Khatam', true)
    else if TestCharacters('03_Khatam') then Khatam = PrepareUnit('03_Khatam', true);
    Gladstone = PrepareUnit('Gladstone', true);
    DeltaDoctor = PrepareUnit('DeltaDoctor', true);
    if TestCharacters('08_Frank') = 1 then Kikuchi = PrepareUnit('Yamoko', true);

    others09 = CreateCharacterSet('09_others');
    ape09 = CreateCharacterSet('09_apes');

    PlaceUnitArea(Simms, CathyTeamSpawn, false);
    PlaceUnitArea(Denis, CathyTeamSpawn, false);
    PlaceUnitArea(Joan, CathyTeamSpawn, false);
    PlaceUnitArea(Khatam, CathyTeamSpawn, false);
    PlaceUnitArea(Gladstone, CathyTeamSpawn, false);
    PlaceUnitArea(DeltaDoctor, CathyTeamSpawn, false);
    PlaceUnitArea(Kikuchi, CathyTeamSpawn, false);

    SetClass([Simms, Khatam, Denis, Gladstone, DeltaDoctor, Joan], 4);

    if others09 > 2 then
    begin
        for i := 1 to 3 do
        begin
            PlaceUnitArea(others09[i], CathyTeamSpawn,  false);
            others09 = Delete(others09, 1);
            SetClass(others09[i], 2);
        end;
    end else
    begin
        for i in others09 do
        begin
            PlaceUnitArea(i, CathyTeamSpawn, false);
            others09 = others09 diff i;
            SetClass(i, 2);
        end; 
    end;

    if FilterAllUnits([[f_side, 4], [f_class, 2]]) = 0 then
    begin
        tmp = FilterAllUnits([[f_side, 4], [f_class, 2]]) diff [Simms, Khatam, Denis, Gladstone, DeltaDoctor, Joan];

        if not tmp then
           begin
           if Denis then
              SetClass(Denis, 2);
           exit;
           end;

        SetClass(tmp[1], 2);
    end;

    for i in ape09 do PlaceUnitArea(i, CathyTeamSpawn, false);


    uc_side = 1;

    hc_class := 1;
    hc_sex := sex_female;
    hc_gallery := '';
    hc_name := '';
    hc_skills := [1, 7, 1, 0];

    AmScout = CreateHuman;

end;

Export function PrepareCornell;
begin

    ChangeSideFog(4, 4);

    uc_side = 4;
    uc_nation = 1;

    Cornell = PrepareUnit('03_Cornel', true);
    PlaceUnitArea(Cornell, CornellSpawn, false);

    SetLives(Cornell, 350);

    If IsInUnit(JMM) then ComMoveXY(Cornell, GetX(IsInUnit(JMM)), GetY(IsInUnit(JMM)))
    else ComMoveXY(Cornell, GetX(JMM), GetY(JMM));
end;