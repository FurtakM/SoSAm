// Prepare Hero for use
// ident - ident from start.txt - 'JMM' for example
// exist_mode
//   - false if unit not exist in save
//   - true if unit already exist in save
// example: JMM := PrepareUnit('JMM', false);
Export Function PrepareUnit(ident, exist_mode);
var unit;
begin

if exist_mode then
   unit := CreateCharacter(ident)
  else
   unit := NewCharacter(ident);

result := unit;
End;

// example: PrepareTank(1, 1, us_medium_wheeled, engine_combustion, control_manual, us_machine_gun, 80);
Export Function PrepareTank(side, nation, chassis, engine, control, weapon, fuel);
begin
uc_side := side;
uc_nation := nation;

vc_chassis := chassis;
vc_engine := engine;
vc_control := control;
vc_weapon := weapon;
vc_fuel_battery := fuel;

result := CreateVehicle;
End;

Export Function SayX(units, ident);
var i;
begin
result := false;

if not units then
   exit;

for i in units do
    if IsOk(i) then
       begin
       Say(i, ident);
       result := i;
       break;
       end;
End;

// Support function to fast spawn people in buildings - Author: Nefarem
Export function RespawnInside(side, nation, build, skill, class, count);
var i,l, un;
begin

   for i = 1 to count do
   begin

           uc_side = side;
           uc_nation = nation;

           hc_gallery = '';
           hc_name = '';
           PrepareHuman(false, class, skill);

           un = CreateHuman;
           PlaceHumanInUnit(un,build);

   end;

END;


// Function by Serpent
Export Function PrepareSolBun(skill, b);
var un, dir;
begin
     uc_side := GetSide(b);
     uc_nation := GetNation(b);

     PrepareHuman(false, 1, skill);
     un = CreateHuman;

     dir := GetDir(b)  - 3;

     if dir < 0 then dir := 6 + dir;

     SetDir(un, dir);
     PlaceHumanInUnit(un, b);
End;

Export Function InVeh(unit);
var filter, i;
begin
result := false;
filter := FilterAllUnits([[f_side, 1], [f_weapon, us_machine_gun]]);

for i in filter do
    if IsDrivenBy(i) = unit then
       begin
       result := true;
       break;
       end;
End;

Export Function FindFreePlace(blist);
var i;
begin
result := false;

if not blist then
   exit;

for i in blist do
    if UnitsInside(i) < 6 then
       begin
       result := i;
       break;
       end;
End;

Export Function Count(timer, mode);
begin
if not timer then
   exit;

if mode in ['asc', 'up', '+'] then
   result := timer + 0$01
  else
   result := timer - 0$01;
End;