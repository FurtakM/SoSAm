// Prepare Hero for use
// ident - ident from start.txt - 'JMM' for example
// exist_mode
//   - false if unit not exist in save
//   - true if unit already exist in save
// example: JMM := PrepareUnit('JMM', false);
Export Function PrepareUnit(ident, exist_mode);
var unit;
begin

if exist_mode then
   unit := CreateCharacter(ident)
  else
   unit := NewCharacter(ident);

result := unit;
End;

// example: PrepareTank(1, 1, us_medium_wheeled, engine_combustion, control_manual, us_machine_gun, 80);
Export Function PrepareTank(side, nation, chassis, engine, control, weapon, fuel);
begin
uc_side := side;
uc_nation := nation;

vc_chassis := chassis;
vc_engine := engine;
vc_control := control;
vc_weapon := weapon;
vc_fuel_battery := fuel;

result := CreateVehicle;
End;


Export Function SayX(units, ident);
var i;
begin
result := false;

if not units then
   exit;

for i in units do
    if IsOk(i) then
       begin
       Say(i, ident);
       result := i;
       break;
       end;
End;

Export Function SpawnApeman(area, amount);
var i;
begin
InitUc;
InitHc;

uc_side := 0;
uc_nation := 0;

for i = 1 to amount do
    begin
    PrepareHuman(false, class_apeman, 1);
    PlaceUnitArea(CreateHuman, area, false);
    end;
End;


Export Function Attack(unit, enemy_list);
var target, targets_list;
begin
if not unit then
   exit;

if IsInUnit(unit) then
   unit := IsDrivenBy(unit);

targets_list := [];


target := UnitFilter(enemy_list, [[f_lives, 250], [f_not, [f_weapon, us_cargo_bay]], [f_type, unit_vehicle], [f_empty]]);

if GetDistUnits(NearestUnitToUnit(target, unit), unit) < 11 and not IsInUnit(unit) then
   begin
   ComEnterUnit(unit, NearestUnitToUnit(target, unit));
   exit;
   end;

targets_list := [UnitFilter(enemy_list, [[f_type, unit_human], [f_not, [f_inside]]]),
                 UnitFilter(enemy_list, [f_type, unit_vehicle]),
                 UnitFilter(enemy_list, [[f_type, unit_building], [f_not, [f_empty]]]),
                 enemy_list
                 ];

for target in targets_list do
    begin
    if target then
       if GetTag(unit) = 11 and (GetType(NearestUnitToUnit(target, unit)) = unit_building or GetWeapon(NearestUnitToUnit(target, unit)) = us_cargo_bay) then
          begin
          ComPlaceDelayedCharge(unit, 0, 0, NearestUnitToUnit(target, unit));
          AddComWait(unit, 0$02);
          end
         else
          ComAttackUnit(unit, NearestUnitToUnit(target, unit));
    end;
End;